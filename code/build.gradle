// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0-rc2'
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def javaProjects = []
def androidProjects = []

javaProjects << 'business'
androidProjects << 'client:android'

project(javaProjects) {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_8
}

project(androidProjects) {
    apply plugin: 'com.android.application'
}

project(javaProjects + androidProjects) {
    group = 'gq.baijie.android.trymodule'
    version = '0.1.0'

    repositories {
        jcenter()
    }

    dependencies {
        compile 'io.reactivex:rxjava:1.1.10'
        // test
        testCompile 'junit:junit:4.12'
    }
}

// for Dagger 2
def daggerVersion = '2.7'
project(javaProjects + androidProjects) {
    dependencies {
        compile "com.google.dagger:dagger:$daggerVersion"
    }
}
project(javaProjects) {
    dependencies {
        compileOnly "com.google.dagger:dagger-compiler:$daggerVersion"
    }
}
project(androidProjects) {
    dependencies {
        provided "com.google.dagger:dagger-compiler:$daggerVersion"
    }
}
// for Dagger 2 end

// for lambda support
project(javaProjects + androidProjects) {
    apply plugin: 'me.tatarka.retrolambda'
}
project(androidProjects) {
    android {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
}
// for lambda support end



Iterable project(Iterable<String> projects, Closure configureClosure) {
    projects.collect {project(it, configureClosure)}
}
